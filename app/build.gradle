buildscript {
    repositories {
        mavenCentral()
        mavenCentral()
        maven { url 'https://plugins.gradle.org/m2/' }
    }
    dependencies {
        classpath 'gradle.plugin.com.onesignal:onesignal-gradle-plugin:0.13.4'
    }
}

repositories {
    mavenCentral()
}

apply plugin: 'com.onesignal.androidsdk.onesignal-gradle-plugin'
apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'com.google.firebase.crashlytics'
apply plugin: 'androidx.navigation.safeargs.kotlin'
apply plugin: 'kotlin-android-extensions'

ext.buildToolsVersion = '30.0.3'
ext.minimumSdkVersion = 21
ext.targetSdkVersion = 31
ext.compileSdkVersion = 31

ext.isSnapshot = false

android {
    compileSdkVersion project.ext.compileSdkVersion
    buildToolsVersion project.ext.buildToolsVersion

    defaultConfig {
        applicationId "com.africell.africell"
        minSdkVersion project.ext.minimumSdkVersion
        targetSdkVersion project.ext.targetSdkVersion

        multiDexEnabled true

        manifestPlaceholders["enableFirebaseCrashlytics"] = true

    }

    buildTypes {
        debug {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            testProguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguardTest-rules.pro'
            manifestPlaceholders["enableFirebaseCrashlytics"] = false
        }
        release {
            minifyEnabled false//TODO set to true when confident
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            testProguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguardTest-rules.pro'
        }


    }
    flavorDimensions "scope"

    productFlavors {
        uganda {
            def versionMajor = 1
            def versionMinor = 0
            def versionPatch = 0
            versionCode generateVersionCode(versionMajor, versionMinor, versionPatch)
            versionName generateVersionName(versionMajor, versionMinor, versionPatch)
            applicationId "com.africell.africell.uganda"
            dimension "scope"
            resValue 'string', 'onesignal_app_id', "e9af51e4-f06c-4249-b281-091470591362"
            resValue 'string', 'app_version_name', "$versionName"

            buildConfigField 'Boolean', 'MULTI_LANG', 'false'
        }
        gambia {
            def versionMajor = 1
            def versionMinor = 0
            def versionPatch = 5
            versionCode generateVersionCode(versionMajor, versionMinor, versionPatch)
            versionName generateVersionName(versionMajor, versionMinor, versionPatch)
            applicationId "com.africell.africell.africellapp"
            dimension "scope"
            resValue 'string', 'onesignal_app_id', "8251e44f-a70c-4402-a1d4-d8a37fe2758d"
            resValue 'string', 'app_version_name', "$versionName"

            buildConfigField 'Boolean', 'MULTI_LANG', 'false'
        }
        sl {
            def versionMajor = 1
            def versionMinor = 2
            def versionPatch = 2
            versionCode generateVersionCode(versionMajor, versionMinor, versionPatch)
            versionName generateVersionName(versionMajor, versionMinor, versionPatch)
            applicationId "com.africell.africell.africellSLApp"
            dimension "scope"
            resValue 'string', 'onesignal_app_id', "c933598b-0a8f-4bb3-9864-032193f92511"
            resValue 'string', 'app_version_name', "$versionName"

            buildConfigField 'Boolean', 'MULTI_LANG', 'false'
        }
        drc {
            def versionMajor = 1
            def versionMinor = 0
            def versionPatch = 0
            versionCode generateVersionCode(versionMajor, versionMinor, versionPatch)
            versionName generateVersionName(versionMajor, versionMinor, versionPatch)
            applicationId "com.africell.africell.drc"
            dimension "scope"
            resValue 'string', 'onesignal_app_id', "f012444e-6865-4e00-adf4-dcac979d95f1"
            resValue 'string', 'app_version_name', "$versionName"

            buildConfigField 'Boolean', 'MULTI_LANG', 'true'
        }
    }

    lintOptions {
        textReport true
        textOutput 'stdout'
        warning 'UnusedResources'
    }

    packagingOptions {
        exclude 'LICENSE.txt'
        //exclude 'META-INF/rxjava.properties'
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = "1.8"
    }

    buildFeatures {
        viewBinding true
    }
}

private Integer generateVersionCode(versionMajor, versionMinor, versionPatch) {
    return ext.minimumSdkVersion * 10000000 + versionMajor * 10000 + versionMinor * 100 + versionPatch
}

private String generateVersionName(versionMajor, versionMinor, versionPatch) {
    return "${versionMajor}.${versionMinor}.${versionPatch}"
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    //implementation "androidx.multidex:multidex:2.0.1"//fixme sdk 21 and above do not need the support version (also MultiDex.install() is not needed)
    implementation "androidx.navigation:navigation-fragment-ktx:$navigationVersion"
    implementation "androidx.navigation:navigation-ui-ktx:$navigationVersion"

    implementation "com.google.firebase:firebase-analytics-ktx:21.2.0"
    implementation "com.google.firebase:firebase-crashlytics-ktx:18.3.1"

    //ui
    implementation "androidx.appcompat:appcompat:1.4.1"
    implementation "androidx.recyclerview:recyclerview:1.2.1"
    implementation "com.google.android.material:material:1.6.0"
    implementation "androidx.constraintlayout:constraintlayout:2.1.4"
    implementation "androidx.preference:preference-ktx:1.2.0"
    implementation "androidx.cardview:cardview:1.0.0"
    implementation("com.benitobertoli:liv:0.3.0", {
        exclude group: 'com.android.support'
    })


    def lifecycleVersion = "2.5.1"
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:$lifecycleVersion"
    implementation "androidx.lifecycle:lifecycle-livedata-ktx:$lifecycleVersion"
    implementation "androidx.lifecycle:lifecycle-common-java8:$lifecycleVersion"

    implementation "androidx.dynamicanimation:dynamicanimation-ktx:1.0.0-alpha03"

    // remote
    def okHttpVersion = '5.0.0-alpha.9'
    implementation "com.squareup.okhttp3:okhttp:$okHttpVersion"
    implementation "com.squareup.okhttp3:okhttp-urlconnection:$okHttpVersion"
    implementation "com.squareup.okhttp3:logging-interceptor:$okHttpVersion"
    def retrofitVersion = '2.9.0'
    implementation "com.squareup.retrofit2:retrofit:$retrofitVersion"
    implementation "com.squareup.retrofit2:converter-gson:$retrofitVersion"
    implementation "com.squareup.retrofit2:adapter-rxjava2:$retrofitVersion"
    def frescoVersion = '2.6.0'
    implementation "com.facebook.fresco:fresco:$frescoVersion"
    implementation "com.facebook.fresco:imagepipeline-okhttp3:$frescoVersion"

    // rx
    implementation "io.reactivex.rxjava2:rxandroid:2.1.1"
    implementation "io.reactivex.rxjava2:rxjava:2.2.21"
    implementation "com.jakewharton.rxbinding2:rxbinding:2.2.0"
    implementation "com.f2prateek.rx.preferences2:rx-preferences:2.0.1"
    implementation "com.jakewharton.rxrelay2:rxrelay:2.1.1"

    // di
    implementation "com.google.dagger:dagger:$daggerVersion"
    implementation "com.google.dagger:dagger-android-support:$daggerVersion"
    kapt "com.google.dagger:dagger-android-processor:$daggerVersion"
    kapt "com.google.dagger:dagger-compiler:$daggerVersion"

    // util
    implementation "com.jakewharton.timber:timber:$timberVersion"

    // push
    implementation "com.onesignal:OneSignal:4.8.1"

    testImplementation "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"
    implementation "me.relex:circleindicator:2.1.6"
    implementation 'com.googlecode.libphonenumber:libphonenumber:8.12.52'

    //google play
    implementation "com.google.android.gms:play-services-location:$googlePlayServicesLocationVersion"
    implementation "com.google.android.gms:play-services-maps:$googlePlayServicesMapsVersion"
    implementation "com.google.android.gms:play-services-gcm:$googlePlayServicesGCMVersion"
    implementation 'com.yarolegovich:discrete-scrollview:1.5.1'
}

apply plugin: 'com.google.gms.google-services'
